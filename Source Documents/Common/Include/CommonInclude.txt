sub CommonInit 
/**********************************************************************************************************************************************************
	File:		CommonInclude.txt
	Purpose:	Common Initialization and Common Ending routines for all QVFs and QVWs that invoke TSEEQ, The Structured ETL Engine for Qlik.
	
	Author:		Jeff R. Robbins
	Email:		jrs@qlik.com; jr@QlikPerf.com
	
	Version:	4.0.7
	Date:		2020-07-13
	
	This program is provided in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  
**********************************************************************************************************************************************************/
	Trace 'Entering subroutine CommonInit in $(vCommonScriptPath)\CommonInclude.txt';
	SET ThousandSep=',';
	SET DecimalSep='.';
	SET MoneyThousandSep=',';
	SET MoneyDecimalSep='.';
	SET MoneyFormat='$#,##0;($#,##0)';
	SET TimeFormat='h:mm:ss TT';
	SET DateFormat='MM/DD/YYYY';
	SET TimestampFormat='YYYY-MM-DD h:mm:ss[.fff] TT';
	SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
	SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';

	IF len(trim(vLVR_VariableDefinition)) > 0 THEN		// 2108-12-09 JRR added this block
		// vLVR_VariableDefinition should not defined at this point, but if it is defined and contains 
		// unbalanced parens, quotes or brackets, it will cause problems with the subsequent include of 
		// $(vCommonScriptPath)load variables From Resident.txt
		SET vLVR_VariableDefinition=;
	ENDIF
	
	IF upper (vPurpose) <> 'APP' AND vTSEEQ_Combined_Purpose_Flag <> 1 THEN 
		SET HidePrefix=''; 
	ELSE
		SET HidePrefix='%'; // hide (by default) field names that have names starting with a percent sign.
	ENDIF
	SET vFinished = 0;  // 0 means script did not finish; 1 inidicates script completion; this is for debugging. 

	/************************************************************************************************************************************
		Get start time of this reload script. 
	************************************************************************************************************************************/
	LET vReloadTimeStart = Now(1);
	/*******************************************************************************************************************************
		Write debugging info to log for this QVW/QVF:
	*******************************************************************************************************************************/
	TRACE '$(vEnvironment)';
	TRACE '$(vApp)';
	TRACE '$(vPurpose)';
	IF TSEEQ_Combined_Purpose_Flag = 1 THEN 
		TRACE 'This is a combined purpose (both LOAD and APP) QVF';
	ENDIF
	/**************************************************************************************************************************************
		If table VariableRepository exists, then drop it, as we'll create it from scratch when we load common variables:
	***************************************************************************************************************************************/
	if len ( NoOfRows('VariableRepository') ) > 0 THEN 
		DROP Table VariableRepository;
	ENDIf
	/**************************************************************************************************************************************
		If table Rules table exists, then drop it, as we don't want to inherit Rules in the case of BINARY LOAD:
	**************************************************************************************************************************************/
	if len ( NoOfRows('Rules For Entire Reload') ) > 0 THEN 
		DROP Table [Rules For Entire Reload];
	ENDIf
	/**************************************************************************************************************************************
		If execution trace table exists, drop it, as we don't need to inherit it in the case of BINARY LOAD:
	**************************************************************************************************************************************/
	if len ( NoOfRows('Execution Stats For Entire Reload') ) > 0 THEN 
		trace SEEQ_ExecutionTracepresent;
		DROP Table [Execution Stats For Entire Reload];
	 ENDIF
	/**************************************************************************************************************************************
		Define the common variables path.
	***************************************************************************************************************************************/
	SET vCommonVariablePath='$(vCommonPath)variables$(vSlash)';
	trace $(vCommonVariablePath)Common_Variables.xls;
	
	// set vPurge=1; // set to 1 to purge.
	if vPurge = 1 THEN // reload without data to quickly create a small QVW. Useful for automated backups and archiving.
		LET vPurge = 0;	// set vPurge to 0 so that the next reload will be a normal (non-purge) reload.
		LET vReloadTimeEnd = now();
		exit script;
	endif
	Trace 'Exiting subroutine CommonInit in $(vCommonScriptPath)\CommonInclude.txt';
end sub

sub CommonEnd

	Trace 'Entering subroutine CommonEnd in $(vCommonScriptPath)\CommonInclude.txt';
	if upper(vPurpose) = 'LOAD' OR vTSEEQ_Combined_Purpose_Flag = 1 THEN 
		RENAME FIELD [QVD Created] to [QVD Loaded];
	endif

	IF upper(vPurpose) = 'APP' OR vTSEEQ_Combined_Purpose_Flag = 1 THEN
		trace 'TSEEQ_RenameMap applied';
		TSEEQ_RenameMap:  // prefix system fields with '%' so they are invisible to self-service users
		Mapping LOAD
		    Old,
		    New
//		FROM [$(vCommonEXDPath)\XLS\FieldRenameMap.xlsx]	// 2022-07-07 JRR commented this line		
		FROM [$(vCommonEXDPath)XLS/FieldRenameMap.xlsx]	// 2022-07-07 JRR added this line for  SaaS / CloudBucket support
		(ooxml, embedded labels, table is Sheet1)
		WHERE ENABLED='Y'
		;
		Rename Fields using TSEEQ_RenameMap;
	ENDIF

	LET vReloadTimeEnd = now();		// for performance profiling
	SET vFinished = 1;  			// 0 means script did not finish; 1 indicates script completion; this is for debugging. 
	Trace 'Exiting subroutine CommonEnd in $(vCommonScriptPath)\CommonInclude.txt';
	EXIT Script;	

	Trace 'Exiting subroutine CommonEnd in $(vCommonScriptPath)\CommonInclude.txt';

ENDSUB	

sub SetRuleSourceFile
	Trace 'Entering subroutine SetRuleSourceFile in $(vCommonScriptPath)\CommonInclude.txt';
	// Define which rules source file to use based on the values of vAppPath, vApp, vPurpose, vQlikProduct and vTSEEQ_RulesFileFormat:
	IF vQlikProduct = 'VIEW' THEN
		IF vTSEEQ_RulesFileFormat='XLS' THEN
			SET vRulesSourceFile='$(vAppPath)ETL_Rules\$(vApp)$(vPurpose)Rules.xls'; 
		ELSEIF vTSEEQ_RulesFileFormat='CSV' THEN
			SET vRulesSourceFile='$(vAppPath)ETL_Rules\CSV\$(vApp)$(vPurpose)Rules_TABLES_Worksheet.csv';    
		ENDIF
	ELSEIF vQlikProduct = 'SENSE' THEN
		IF vTSEEQ_RulesFileFormat='XLS' THEN
			SET vRulesSourceFile='lib://$(vAppPath)ETL_Rules/$(vApp)$(vPurpose)Rules.xls';        
		ELSEIF  vTSEEQ_RulesFileFormat='CSV' THEN
			SET vRulesSourceFile='lib://$(vAppPath)ETL_Rules/CSV/$(vApp)$(vPurpose)Rules_TABLES_Worksheet.csv';        
		ENDIF
	ENDIF    

	Trace 'Exiting subroutine SetRuleSourceFile in $(vCommonScriptPath)\CommonInclude.txt';
	/*******************************************************************************************************************
		Note that since each CSV file can only contain a single table, if vTSEEQ_RulesFileFormat='CSV', then
		the EXTRACT and TRANSFORM steps each read multiple CSV files. 
		
		Specifically, the table-level rules are in  files named *_TABLES_Worksheet.csv, and the field-level rules are
		in files named *_EXCLUDE_FIELDS_Worksheet.csv, *_INCLUDE_FIELDS_Worksheet.csv and *_TRANSFORM_FIELDS_Worksheet.csv.
		
		For simplicity, we need only to specify the table-level rules file; field-level rule file names are automatically
		derived by TSEEQ based on the name of the table-level file.

		Since each single XLS file contains multiple tables, one per Excel worksheet, then if vTSEEQ_RulesFileFormat='XLS',
		only a single rules file is needed. One XLS file contains both table-level and field-level rules.
	*********************************************************************************************************************/
end sub